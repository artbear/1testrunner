#Использовать asserts

Перем юТест;
Перем Счетчик;
Перем ФайлИнициализации;
Перем КонтрольноеЗначение;

&Инициализация
Процедура ПередЗапускомТестов() Экспорт
	
	ФайлИнициализации = ПолучитьИмяВременногоФайла("init");
	КонтрольноеЗначение = СтрШаблон("init_%1", Новый УникальныйИдентификатор());

	Текст = Новый ТекстовыйДокумент();
	Текст.УстановитьТекст(КонтрольноеЗначение);
	Текст.Записать(ФайлИнициализации);

	Сообщить(СтрШаблон("Инициализированы тесты ""%1""", ТекущийСценарий().Источник));
КонецПроцедуры

&Перед
Процедура ПередЗапускомТеста() Экспорт
	Счетчик = Новый Массив;
КонецПроцедуры

&Перед
Процедура ПередЗапускомТестаПервыйРаз() Экспорт
	Счетчик.Добавить("1");
КонецПроцедуры

&ПереД
Процедура ПередЗапускомТестаВторойРаз() Экспорт
    Счетчик.Добавить("2");
КонецПроцедуры
	
&После
Процедура ПослеЗапускаТестаВторойРаз() Экспорт
	Сообщить("ТестОкончил");
КонецПроцедуры

&Завершение
Процедура ПослеЗапускаТестов() Экспорт
	Текст = Новый ТекстовыйДокумент();
	Текст.Прочитать(ФайлИнициализации);
	Значение = СокрЛП(Текст.ПолучитьТекст());
	
	Утверждения.ПроверитьРавенство(Значение, КонтрольноеЗначение);

	УдалитьФайлы(ФайлИнициализации);

	Сообщить(СтрШаблон("Завершены тесты ""%1""", ТекущийСценарий().Источник));
КонецПроцедуры

&ЗавершеНИе
Процедура ПослеЗапускаТестовВторойРаз() Экспорт

	КонтрольныйФайл = Новый Файл(ФайлИнициализации);

	Утверждения.ПроверитьЛожь(КонтрольныйФайл.Существует());

	Сообщить(СтрШаблон("Совсем завершены тесты ""%1""", ТекущийСценарий().Источник));

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьВерсию() Экспорт
	Утверждения.ПроверитьРавенство("0.1", Версия());
	Ожидаем.Что(Счетчик.Количество()).Равно(2);
КонецПроцедуры

&тесТ
Процедура ТестДолжен_ПроверитьВерсиюЕщеРаз() Экспорт
	Утверждения.ПроверитьРавенство("0.1", Версия());
КонецПроцедуры

&Тест
&Параметры(1, 2, Ложь)
&Параметры(1, 1, Истина)
&Параметры("10", "10", Истина)
&Параметры(65465, "10", Ложь)
Процедура ТестДолжен_ВыполнитьсяСПараметрами(ПервоеЗначение, ВтороеЗначение, Ожидание) Экспорт
	Результат = (ПервоеЗначение = ВтороеЗначение);
	Утверждения.ПроверитьРавенство(Ожидание, Результат);
КонецПроцедуры

Функция Версия()
	Возврат "0.1";
КонецФункции
